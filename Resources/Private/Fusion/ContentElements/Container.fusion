prototype(Jonnitto.ImagesInARow:Container) < prototype(Neos.Neos:ContentCollection) {
    baseWidth = ${Configuration.setting('Jonnitto.ImagesInARow.baseWidth')}
    container = ${Configuration.setting('Jonnitto.ImagesInARow.container')}
    nodePath = ''

    childCount = ${q(node).children('[instanceof Jonnitto.ImagesInARow:Image]').count()}
    dimensionsWidth = ${this.childCount ? this.baseWidth / this.childCount : this.baseWidth}

    contentElement = true
    namespace = 'jonnitto-imagesinarow'

    @context {
        container = ${this.container}
        dimensionsWidth = ${this.dimensionsWidth}
        contentElement = ${this.contentElement}
        namespace = ${this.namespace}
    }

    attributes{
        data-warning = ${node.context.inBackend ? Translation.translate('noImages', 'Please add some images', [], 'NodeTypes/Container', 'Jonnitto.ImagesInARow') : false}
        class = Neos.Fusion:RawArray {
            namespace = ${container || node.context.inBackend ? false : namespace}
            row = ${BEM.string(namespace, 'row')}
        }
    }

    @process {
        containerWrapping = Neos.Fusion:Tag {
            @if.needWrapping = ${container || node.context.inBackend}
            attributes.class = Neos.Fusion:RawArray {
                namespace = ${namespace}
                imagesinarow = ${container ? BEM.string(namespace, 'container') : false}
                container = ${Type.isString(container) ? container : false}
            }
            content = ${value}
        }
        contentElementWrapping {
            @if.contentElementWrapping = ${contentElement}
            expression = Neos.Neos:ContentElementWrapping
            @position = 'end 999999999'
        }
    }
    @exceptionHandler = 'Neos\\Neos\\Fusion\\ExceptionHandlers\\NodeWrappingHandler'
    
    @cache {
        mode = 'cached'
        entryIdentifier.node = ${node}
        entryTags {
            1 = ${Neos.Caching.nodeTag(node)}
            2 = ${Neos.Caching.descendantOfTag(node)}
        }
    }
}
