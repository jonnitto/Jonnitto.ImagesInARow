prototype(Jonnitto.ImagesInARow:Container) < prototype(Neos.Fusion:Tag) {
    baseWidth = ${Configuration.setting('Jonnitto.ImagesInARow.baseWidth')}
    container = ${Configuration.setting('Jonnitto.ImagesInARow.container')}

    childCount = ${q(node).children('media').children().count()}
    dimensionsWidth = ${this.childCount ? this.baseWidth / this.childCount : this.baseWidth}

    @context {
        container = ${this.container}
        dimensionsWidth = ${this.dimensionsWidth}
        baseClass = 'jonnitto-imagesinarow'
    }

    attributes.class = Neos.Fusion:RawArray {
        nodetype = ${baseClass}
        container = ${Type.isString(container) ? container : false}
    }

    content = Neos.Neos:ContentCollection {
        nodePath = 'media'
        attributes.class = ${baseClass + '__row'}
    }

    @process.contentElementWrapping {
        expression = Neos.Neos:ContentElementWrapping
        @position = 'end 999999999'
    }
    @exceptionHandler = 'Neos\\Neos\\Fusion\\ExceptionHandlers\\NodeWrappingHandler'
}
