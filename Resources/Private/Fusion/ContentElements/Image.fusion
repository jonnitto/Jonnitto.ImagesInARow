prototype(Jonnitto.ImagesInARow:Image) < prototype(Neos.Neos:ContentComponent) {
    image = ${q(node).property('image')}
    ratio = ${this.image && this.image.height ? (this.image.width / this.image.height) : 1.5}
    width = ${dimensionsWidth ? Math.ceil(dimensionsWidth * this.ratio) : baseWidth}
    width.@process.maximumWidthFromBase = ${Math.min(value, baseWidth)}

    alternativeText = ${q(node).property('alternativeText')}
    title = ${q(node).property('title')}
    link = ${q(node).property('link')}
    lightbox = ${q(node).property('lightbox')}
    lightboxCaption = ${q(node).property('caption')}

    imageSourceSettings = Neos.Fusion:DataStructure {
        dummyBaseWidth = 300
        dummyBaseHeight = 200
        dummyText = ${Translation.translate('ui.label', 'Image', [], 'NodeTypes/Image', 'Jonnitto.ImagesInARow') }
    }

    gutter = ${gutter}
    lazy = ${lazy}
    type = ${renderAsPicture ? 'picture' : 'image'}
    srcset = '1x,2x'
    style = 'width:100%;max-width:none;height:auto;display:block;object-fit:fill;'

    renderer = afx`
        <Neos.Fusion:Augmenter class='jonnitto-imagesinarow__image' style={'flex:' + props.ratio + ' 1 0%;margin:' + props.gutter / 2 + 'px;padding:0;display:block;max-width: 100%;'}>
            <Carbon.Image:Component.Case {...props} />
        </Neos.Fusion:Augmenter>
    `
}
