prototype(Jonnitto.ImagesInARow:Image) < prototype(Neos.Neos:ContentComponent) {
    image = ${q(node).property('image')}
    ratio = ${this.image && this.image.height ? (this.image.width / this.image.height) : 1.5}
    width = ${dimensionsWidth ? Math.ceil(dimensionsWidth * this.ratio) : baseWidth}
    width.@process.maximumWidthFromBase = ${Math.min(value, baseWidth)}

    alternativeText = ${q(node).property('alternativeText')}
    link = ${q(node).property('link')}
    lightbox = ${q(node).property('lightbox')}
    lightboxCaption = ${q(node).property('caption')}

    imageSourceSettings = Neos.Fusion:DataStructure {
        dummyBaseWidth = 300
        dummyBaseHeight = 200
        dummyText = ${Translation.translate('ui.label', 'Image', [], 'NodeTypes/Image', 'Jonnitto.ImagesInARow') }
    }

    lazy = ${lazy}
    renderAsPicture = ${renderAsPicture}
    srcset = '1x,2x'

    renderer = afx`
        <Neos.Fusion:Augmenter
            class='jonnitto-imagesinarow__image'
            style={'--ratio:' + props.ratio}
        >
            <Carbon.Image:Component.Image @if={!props.renderAsPicture} {...props} />
            <Carbon.Image:Component.Picture @if={props.renderAsPicture} {...props} />
        </Neos.Fusion:Augmenter>
    `
}
